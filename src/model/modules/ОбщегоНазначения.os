#Использовать json

перем ПарсерJSON;

Функция РазобратьПараметрыЗапроса(Знач Параметры) Экспорт
	
	Результат = Новый Соответствие;
	
	Пары = СтрРазделить(Сред(Параметры,2), "&");
	Для Каждого Пара Из Пары Цикл
		Поз = СтрНайти(Пара, "=");
		Если Поз <> 0 Тогда
			Ключ = Лев(Пара, Поз-1);
			Значение = Сред(Пара, Поз+1);
			Результат.Вставить(Ключ, Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьОбъектТелаЗапроса(Данные) Экспорт
	
	Лог = НастройкиПриложения.Лог;

	ПотокЧтения = Данные.ПолучитьПотокТолькоДляЧтения();
	Чтение = Новый ЧтениеДанных(ПотокЧтения);
	ДД = Чтение.Прочитать().ПолучитьДвоичныеДанные() ; 	
	ИмяФайла = ПолучитьИмяВременногоФайла();
	файл = Новый Файл(ИмяФайла);
	ДД.Записать(ИмяФайла);
	
	Лог.Отладка("Имя временного файла - %1", ИмяФайла);
	
	Текст = Новый ТекстовыйДокумент; 
	Текст.Прочитать(ИмяФайла, КодировкаТекста.UTF8);
	ТекстТелаЗапроса = Текст.ПолучитьТекст(); 
	
	Лог.Отладка("Текст тела запроса - %1", ТекстТелаЗапроса);
	
	УдалитьФайлы(ИмяФайла);

	ОбъектЗапрос = ПарсерJSON.ПрочитатьJSON(ТекстТелаЗапроса);

	Возврат ОбъектЗапрос;
	
КонецФункции

Функция ПолучитьЦифрыТелефона(СтрокаТелефона) Экспорт
	
	Если ТипЗнч(СтрокаТелефона) <> Тип("Строка") Тогда
		Возврат "";
	КонецЕсли;
	
	ЦифрыТелефона = "";
	
	Для ПозицияСимвола = 1 По СтрДлина(СтрокаТелефона) Цикл
		КодСимвола = КодСимвола(Сред(СтрокаТелефона, ПозицияСимвола, 1));
		Если КодСимвола >= 48 И КодСимвола <= 57 Тогда
			ЦифрыТелефона = ЦифрыТелефона + Символ(КодСимвола);
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ЦифрыТелефона;
	
КонецФункции

Функция ВремяВыполнения(Секунды) Экспорт

    Часов=Цел(Секунды/3600);
    СекОстМин= Секунды % 3600;    
    Минут=Цел(СекОстМин/60);
    Сек=СекОстМин % 60;
    Если Часов<10 Тогда
        Часов="0"+СокрЛП(Часов);
    Иначе
        Часов=СокрЛП(Часов);    
    КонецЕсли;
    Если Минут<10 Тогда
        Минут="0"+СокрЛП(Минут);
    Иначе
        Минут=СокрЛП(Минут);    
    КонецЕсли;
    Если Сек<10 Тогда
        Сек="0"+СокрЛП(Сек);
    Иначе
        Сек=СокрЛП(Сек);    
    КонецЕсли;

    Время=Часов+":"+Минут+":"+Сек;

    Возврат Время;

КонецФункции

ПарсерJSON = Новый ПарсерJSON;